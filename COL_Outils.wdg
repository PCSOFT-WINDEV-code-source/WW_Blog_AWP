#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : COL_Outils
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x11dd8d9d0125bf43
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  internal_properties : CAAAAAgAAABPOo5wfzL5PtlmH9g6pVlcqG1md+uM4GM8B/k/aVja5R6ZseBzxTGWZbo12UeE8bWym5Rl91AKrNq9wTYnFyamg2lAMw==
  type_code : 31
  p_codes : []
  procedures :
   -
     name : FormateChainePourURL
     procedure_id : 1287340834096084632
     type_code : 15
     code : |1+
      
      // Résumé : Formate une chaine pour l'URL rewriting
      // Syntaxe :
      //[ <Résultat> = ] FormateChainePourURL (<sChaine> est chaîne)
      //
      // Paramètres :
      //	sChaine (chaîne ANSI) : Chaine à formater
      // Valeur de retour :
      // 	chaîne ANSI : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCEDURE FormateChainePourURL(sChaine est une chaine)
      
      sChaineFormatée est une chaine
      
      sChaineFormatée = SansAccent(sChaine)
      sChaineFormatée = Remplace(sChaineFormatée," ","_")
      sChaineFormatée = Remplace(sChaineFormatée,"#","")
      sChaineFormatée = Remplace(sChaineFormatée,"-","")
      sChaineFormatée = Remplace(sChaineFormatée,"%","")
      sChaineFormatée = Remplace(sChaineFormatée,"?","")
      sChaineFormatée = Remplace(sChaineFormatée,"&","")
      sChaineFormatée = Remplace(sChaineFormatée,":","")
      sChaineFormatée = Remplace(sChaineFormatée,",","")
      sChaineFormatée = Remplace(sChaineFormatée,";","")
      sChaineFormatée = Remplace(sChaineFormatée,"""","")
      
      RENVOYER sChaineFormatée
     type : 458752
   -
     name : DonneGravatar
     internal_properties : CAAAAAgAAABS4wM9QjPPZBI51Y87O2C2iMjffZ8bP2x/gJRQusuH3HMg1CJ2MEfVfvhciqGrq5ug/6695+bc3tBQ5J3YTNCTiZuILUi4y1O7KjQUlPS3jkqwudULs6fcQzEWQZhO5RlpVFiqkG9TqKxjOzYgBMcWYyXeScS/vjkSi4fBzUfnsnZFzjGvPSn91zkJee7KOlcwuCZTK4hQwgdKHad/zBfHS4U2rA5AuUb431P61sNBpeRYlRtwk3AH4l0hSIEm
     procedure_id : 1289176979879069271
     type_code : 15
     code : |1-
      // Résumé : Donne un avatar depuis le service Gravatar
      // Syntaxe :
      //[ <Résultat> = ] DonneGravatar (<sAdresseEmail> est chaîne [, <nTaille> est entier])
      //
      // Paramètres :
      //	sAdresseEmail (chaîne ANSI) : L'adresse e-mail pour laquelle on veut un avatar
      // 	nTaille (entier - valeur par défaut=50) : La taille de l'avatar désiré
      // Valeur de retour :
      // 	chaîne ANSI : L'url de l'avatar
      //
      PROCEDURE DonneGravatar(sAdresseEmail est une chaine, nTaille est un entier = 50)
      
      sURLAvatar est une chaine
      sHash est une chaîne 
      
      // Calcul de la clé de hash
      sHash= BufferVersHexa(HashChaîne(HA_MD5_128,Minuscule(SansEspace(sAdresseEmail))))
      
      // Construction de l'url pour un avatar de 50px
      sURLAvatar = ChaîneConstruit("http://www.gravatar.com/avatar/%1?s=%2&d=mm",Remplace(Minuscule(sHash)," ",""),nTaille)
      
      renvoyer sURLAvatar
     type : 458752
   -
     name : AfficheRSS
     internal_properties : CAAAAAgAAAAogJ3RYD9wrd6ptkx54gUuY4ZRn1mtfzdIKJa+nR9EAVpqbk4XRpaqvryfXOgXGfvcfSfqz0kjnkvuSO6KpnjiA6/0cQLJ72uYZ0e7paeZYeDTnBUhJAmWEKXw1lGybBEj8okbION3MHJtmLt6nMMmQRebthBpDLO650YM6zPRvJ9B4ROgfebDvxQWnhN3ziVJdB3IS5dJAeTDSU2KFQEQItL6Vs7L
     procedure_id : 1289549624212826964
     type_code : 15
     code : |1-
      
      // Résumé : Affiche le flux RSS
      //
      PROCEDURE AfficheRSS()
      
      MonFlux est un rssFlux
      MonCanal est un rssCanal
      MonEntrée est un rssEntrée
      MonIdentifiant est un rssidentifiant
      bPremier est un booléen = vrai
      
      // Renseignements du canal
      MonCanal..Titre = ChargePref(COL_Préférences.BLOG_TITRE,"Mon blog")
      MonCanal..Description = "Flux RSS de " + MonCanal..Titre
      MonCanal..DateMiseAJour = DateSys() + HeureSys()
      MonCanal..DatePublication = DateSys() + HeureSys()
      MonCanal..Lien = ChargePref(BLOG_ADRESSE,SiteAdresse())
      
      REQ_ArticlesPubliés.pRecherche = ""
      HExécuteRequête(REQ_ArticlesPubliés)
      pour TOUT REQ_ArticlesPubliés 
      	Si bPremier ALORS
      		MonCanal..DateMiseAJour = DateSys() + HeureSys()
      		MonCanal..DatePublication = DateSys() + HeureSys()
      		bPremier = faux	
      	FIN
      	
      	// Renseignements de l'article
      	MonEntrée..Auteur = REQ_ArticlesPubliés.NomAffichage
      	MonEntrée..Description = remplace(REQ_ArticlesPubliés.Contenu,"mediasuploads",MonCanal..lien + ["/"] + "../mediasuploads")
      	MonEntrée..Titre = REQ_ArticlesPubliés.Titre
      	MonEntrée.DatePublication = REQ_ArticlesPubliés.DateHeure
      	
      	MonIdentifiant.Valeur = REQ_ArticlesPubliés.IDArticle
      	MonIdentifiant.LienPermanent = faux
      	MonEntrée.Identifiant = MonIdentifiant
      	MonEntrée.Lien = ChargePref(BLOG_ADRESSE,SiteAdresse()) + ["/"] + FormateChainePourURL(REQ_ArticlesPubliés.Titre) + "-" + REQ_ArticlesPubliés.IDArticle + ".awp"
      	
      	// Ajoute l'article dans le canal
      	Ajoute(MonCanal..Entrée, MonEntrée)
      FIN
      
      // Ajoute le canal dans le flux
      Ajoute(MonFlux..Canal, MonCanal)
      
      // Affiche le flux
      rssAffiche(MonFlux)
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_SansNom1
resources :
 string_res :
  identifier : 0x1fca3760084f8612
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
